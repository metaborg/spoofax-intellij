buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'commons-io:commons-io:2.4'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

group 'org.metaborg'
version '2.0.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    provided
}

repositories {
    mavenLocal()
    mavenCentral()
    // This list must match the repositories listed in spoofax-releng/parent/pom.xml and
    // http://www.metaborg.org/dev/maven/#local-settings-file
    maven {
        url "http://artifacts.metaborg.org/content/repositories/releases/"
    }
    maven {
        url "http://artifacts.metaborg.org/content/repositories/snapshots/"
    }
    maven {
        url "https://raw.githubusercontent.com/pluto-build/pluto-build.github.io/master/mvnrepository/"
    }
    maven {
		url "http://sugar-lang.github.io/mvnrepository/"
    }
}

build.dependsOn shadowJar

task(printShadowDependencies, description: 'Prints all included shadow dependencies.') << {
	def r = shadowJar.getIncludedDependencies()
	r.collect { it.name }.sort().each { dep ->
        println "      ${dep}"
    }
}

task artifacts {
    group = "Help"
    description = "Displays the artifacts associated with each configuration of " + project
    doFirst {
        configurations.findAll().each { config ->
            println "${config}:"
            config.allArtifacts.getFiles().each { file -> println "" + file}
            println ' '
        }
    }
}

dependencies {
    // Main dependencies
    compile('org.metaborg:org.metaborg.core:2.0.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
        exclude group: 'com.google.guava'
    }
    compile('org.metaborg:org.metaborg.spoofax.core:2.0.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
        exclude group: 'com.google.guava'
    }
    compile('org.metaborg:org.metaborg.spoofax.meta.core:2.0.0-SNAPSHOT') {
        exclude group: 'org.slf4j'
        exclude group: 'com.google.guava'
    }
    provided 'com.google.guava:guava:17.0'
    provided 'org.slf4j:slf4j-api:1.7.10'
    
    provided 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-vfs2:2.1'
    compile 'com.google.inject:guice:4.0'
    compile 'com.google.inject.extensions:guice-assistedinject:4.0'
    compile 'com.google.inject.extensions:guice-multibindings:4.0'
    compile 'org.javassist:javassist:3.20.0-GA'
}

shadowJar {
    baseName = 'org.metaborg.intellij.deps'
    classifier = ''
    zip64 = true
    
    relocate 'org.apache.commons.io', 'intellij.org.apache.commons.io'
}

sourceSets.main.compileClasspath += configurations.provided

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = 'org.metaborg.intellij.deps'
        }
    }
}
