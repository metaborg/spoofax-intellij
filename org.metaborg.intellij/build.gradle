buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.9"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'kotlin'

    group "org.metaborg"
    version version
    sourceCompatibility = javaVersion
    targetCompatibility = javaTargetVersion

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://artifacts.metaborg.org/content/repositories/releases/"
        }
        maven {
            url "http://artifacts.metaborg.org/content/repositories/snapshots/"
        }
        maven {
            url "https://raw.githubusercontent.com/pluto-build/pluto-build.github.io/master/mvnrepository/"
        }
        maven {
            url "http://sugar-lang.github.io/mvnrepository/"
        }
    }

    dependencies {
        compile     "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    }

    configurations {
        provided
    }

    sourceSets.main.compileClasspath += configurations.provided

    idea {
        module {
            inheritOutputDirs = true
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    eclipse {
        classpath {
            plusConfigurations += [configurations.provided]
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                mavenLocal()
            }
        }
    }
}

configure(allprojects.findAll {it.name != 'org.metaborg.spoofax-deps'}) {
  apply plugin: 'org.jetbrains.intellij'

  intellij {
      type = "IC"
      version ideaVersion
      pluginName 'Spoofax'
      downloadSources Boolean.valueOf(sources)
      sameSinceUntilBuild Boolean.valueOf(isEAP)
      alternativeIdePath localIdePath
  }
}

subprojects { runIde.enabled = false }
runIde.enabled = true

publishPlugin {
    username publishUsername
    password publishPassword
    channels publishChannel
}

// To debug the JPS plugin: enable the "Debug Build Process" action in the sandbox instance
// (Ctrl+Shift+A), then start a build and attach a remote debugger to port 5005.
// https://github.com/JetBrains/gradle-intellij-plugin/issues/23
afterEvaluate {
    tasks.getByName('runIde') {
        jvmArgs += ["-Dcompiler.process.debug.port=5005"]
    }
}

dependencies {
    // Main dependencies
    compile     project(":org.metaborg.spoofax-common")
    compile     (project(":org.metaborg.jps")) {
        // Fixes an issue where the IntelliJ sources cause class loading to fail.
        //   ClassNotFoundException: org.jetbrains.jps.intellilang.model.impl.JpsIntelliLangModelSerializerExtension
        exclude module: "idea"
    }

    testCompile "junit:junit:4.12"
}
