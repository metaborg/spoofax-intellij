buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'

group 'org.metaborg'
version '2.1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
//    maven {
//        url "http://artifacts.metaborg.org/content/repositories/releases/"
//    }
//    maven {
//        url "http://artifacts.metaborg.org/content/repositories/snapshots/"
//    }
}

// FIX: Circular dependency due to adding test class dependencies
// https://issues.gradle.org/browse/GRADLE-2470
subprojects {
    apply plugin: 'java'
}

configurations {
    provided
    language
    languageBaseline
    testLanguage
    testLanguageBaseline

    jpsRuntime {
        //extendsFrom runtime
        exclude group: 'com.jetbrains'
    }
}

intellij {
    //version 'IC-15.0.3'

    // FIXME: hack to support both IDEA 15 and IDEA 16.
    // See https://github.com/intellij-rust/intellij-rust/issues/243
    updateSinceUntilBuild = false
}

// To debug the JPS plugin: enable the "Debug Build Process" action in the sandbox instance
// (Ctrl+Shift+A), then start a build and attach a remote debugger to port 5005.
// https://github.com/JetBrains/gradle-intellij-plugin/issues/23
afterEvaluate {
    task("debugJps", type: org.jetbrains.intellij.RunIdeaTask, group: 'intellij', dependsOn: 'prepareSandbox', description: 'Runs Intellij IDEA with installed plugin and debugger support for JPS on port 5005.') {
        jvmArgs += ["-Dcompiler.process.debug.port=5005"]
    }
}

task copyJpsLibraries(type: Copy, description: 'Copies the JPS libraries to the resources.') {
    into "src/main/resources/jpslib/"
    from configurations.jpsRuntime
}

task copyLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the resources.') {
    into "src/main/resources/languages/"
    from configurations.language
}

task copyTestLanguageDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.') {
    into "src/test/resources/languages/"
    from configurations.testLanguage
}

task copyLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the resources.', dependsOn: copyLanguageDependencies) {
    into "src/main/resources/languages/"
    from configurations.languageBaseline
}

task copyTestLanguageBaselineDependencies(type: Copy, description: 'Copies the language dependencies to the test resources.', dependsOn: copyTestLanguageDependencies) {
    into "src/test/resources/languages/"
    from configurations.testLanguageBaseline
}

processResources.dependsOn copyLanguageBaselineDependencies
processResources.dependsOn copyJpsLibraries
processTestResources.dependsOn copyTestLanguageBaselineDependencies

dependencies {
    compile 'org.metaborg:org.metaborg.intellij.deps:2.1.0-SNAPSHOT'
    compile 'org.slf4j:slf4j-api:1.7.10'
    provided 'com.google.guava:guava:17.0'
    provided 'com.google.code.findbugs:jsr305:3.0.0'
    
    jpsRuntime 'org.slf4j:slf4j-api:1.7.12'

    language 'org.metaborg:org.metaborg.meta.lang.esv:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lang.nabl:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lang.sdf:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lang.template:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lang.ts:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lang.stratego:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lang.analysis:2.1.0-SNAPSHOT@spoofax-language'
    language 'org.metaborg:org.metaborg.meta.lib.analysis:2.1.0-SNAPSHOT@spoofax-language'

    testCompile 'junit:junit:4.12'

    testLanguage 'org.metaborg:org.metaborg.meta.lang.sdf:2.1.0-SNAPSHOT@spoofax-language'
}

sourceSets.main.compileClasspath += configurations.provided

idea {
    module {
        inheritOutputDirs = true
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            mavenLocal()
        }
    }
}
